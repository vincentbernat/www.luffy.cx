---
title: "Customize Caddy's plugins with Nix"
description: |
  Caddy, a web server written in Go, supports compile-time plugin extensions.
  This article explains how to add plugins using Nix and xcaddy.
uuid: 13b93813-1131-4f0e-8d25-ac6424f395a5
tags:
  - distribution-nixos
attachments:
  "https://github.com/vincentbernat/caddy-nix/": "Git repository"
---

[Caddy][] is an open-source web server written in Go. It handles TLS
certificates automatically and comes with a simple configuration syntax. Users
can extend its functionality through [plugins][][^plugins] to add features like
[rate limiting][], [caching][], and [Docker integration][].

[^plugins]: This article uses the term "plugins," though Caddy documentation
    also refers to them as "modules" since they are implemented as Go modules.

While *Caddy* is available in [Nixpkgs][], adding extra plugins is not
simple.[^simple] The compilation process needs Internet access, which [Nix
denies during build][] to ensure [reproducibility][]. When trying to build the
following derivation using [xcaddy][], a tool for building Caddy with plugins,
it fails with this error: `dial tcp: lookup proxy.golang.org on [::1]:53:
connection refused`.

[^simple]: This is a [feature request][] since quite some time. A [proposed
    solution][] has been rejected. The one described in this article is a bit
    different and I have proposed it in another [pull request][].

```nix
{ pkgs }:
pkgs.stdenv.mkDerivation {
  name = "caddy-with-xcaddy";
  nativeBuildInputs = with pkgs; [ go xcaddy cacert ];
  unpackPhase = "true";
  buildPhase =
    ''
      xcaddy build --with github.com/caddy-dns/powerdns@v1.0.1
    '';
  installPhase = ''
    mkdir -p $out/bin
    cp caddy $out/bin
  '';
}
```

Fixed-output derivations are an exception to this rule and get network access
during build. They need to specify their output hash. For example, the
`fetchurl` function produces a fixed-output derivation:

```nix
{ stdenv, fetchurl }:
stdenv.mkDerivation rec {
  pname = "hello";
  version = "2.12.1";
  src = fetchurl {
    url = "mirror://gnu/hello/hello-${version}.tar.gz";
    hash = "sha256-jZkUKv2SV28wsM18tCqNxoCZmLxdYH2Idh9RLibH2yA=";
  };
}
```

To create a fixed-output derivation, you need to set the [`outputHash`
attribute][outputHash]. The example below shows how to output *Caddy*'s source
code, with some plugin enabled, as a fixed-output derivation using `xcaddy` and
`go mod vendor`.

```nix hl_lines="18 19 20"
pkgs.stdenvNoCC.mkDerivation rec {
  pname = "caddy-src-with-xcaddy";
  version = "2.8.4";
  nativeBuildInputs = with pkgs; [ go xcaddy cacert ];
  unpackPhase = "true";
  buildPhase =
    ''
      export GOCACHE=$TMPDIR/go-cache
      export GOPATH="$TMPDIR/go"
      XCADDY_SKIP_BUILD=1 TMPDIR="$PWD" \
        xcaddy build v${version} --with github.com/caddy-dns/powerdns@v1.0.1
      (cd buildenv* && go mod vendor)
    '';
  installPhase = ''
    mv buildenv* $out
  '';

  outputHash = "sha256-F/jqR4iEsklJFycTjSaW8B/V3iTGqqGOzwYBUXxRKrc=";
  outputHashAlgo = "sha256";
  outputHashMode = "recursive";
}
```

With a fixed-output derivation, it is up to us to ensure the output is always
the same:

- we ask `xcaddy` to not compile the program and keep the source code,[^xcaddy]
- we pin the version of Caddy we want to build, and
- we pin the version of each requested plugin.

[^xcaddy]: This is not perfect: if the source code produced by `xcaddy` changes,
    the hash would change and the build would fail.

You can use this derivation to override the `src` attribute in `pkgs.caddy`:

```nix
pkgs.caddy.overrideAttrs (prev: {
  src = pkgs.stdenvNoCC.mkDerivation { /* ... */ };
  vendorHash = null;
  subPackages = [ "." ];
});
```

Check out the complete example in the [GitHub repository][]. To integrate into a
[Flake][], add `github:vincentbernat/caddy-nix` as an overlay:

```nix
{
  inputs = {
    nixpkgs.url = "nixpkgs";
    flake-utils.url = "github:numtide/flake-utils";
    caddy.url = "github:vincentbernat/caddy-nix";
  };
  outputs = { self, nixpkgs, flake-utils, caddy }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs {
          inherit system;
          overlays = [ caddy.overlays.default ];
        };
      in
      {
        packages = {
          default = pkgs.caddy.withPlugins {
            plugins = [ "github.com/caddy-dns/powerdns@v1.0.1" ];
            hash = "sha256-F/jqR4iEsklJFycTjSaW8B/V3iTGqqGOzwYBUXxRKrc=";
          };
        };
      });
}
```

!!! "Update (2024-11)" This flake won't work with Nixpkgs 24.05 or older because
it relies on [this
commit](https://github.com/nixOS/nixpkgs/commit/eed069a5bc40ba4d871de7700c7eb8d592e98cb6)
to properly override the `vendorHash` attribute.

[caddy]: https://caddyserver.com/ "Caddy Web Server"
[plugins]: https://caddyserver.com/docs/modules/ "All registered Caddy plugins"
[rate limiting]: https://github.com/mholt/caddy-ratelimit "HTTP rate limiting module for Caddy"
[caching]: https://github.com/caddyserver/cache-handler "Distributed HTTP caching module for Caddy"
[docker integration]: https://github.com/invzhi/caddy-docker-upstreams "Docker dynamic upstreams for Caddy"
[nixpkgs]: https://github.com/NixOS/nixpkgs/blob/master/pkgs/by-name/ca/caddy/package.nix
[xcaddy]: https://github.com/caddyserver/xcaddy "Build Caddy with plugins"
[reproducibility]: https://zero-to-nix.com/concepts/reproducibility "Zero to Nix: reproducibility"
[nix denies during build]: https://zero-to-nix.com/concepts/hermeticity "Zero to Nix: hermeticity"
[flake]: https://zero-to-nix.com/concepts/flakes "Zero to Nix: flakes"
[outputhash]: https://nix.dev/manual/nix/2.22/language/advanced-attributes.html#adv-attr-outputHash
[github repository]: https://github.com/vincentbernat/caddy-nix/blob/main/examples/xcaddy-src/flake.nix
[feature request]: https://github.com/NixOS/nixpkgs/issues/14671 "caddy: add all addons #14671"
[proposed solution]: https://github.com/NixOS/nixpkgs/pull/317881 "caddy: add support for compiling with Caddy modules (plugins) #317881"
[pull request]: https://github.com/NixOS/nixpkgs/pull/358586 "caddy: add suport for compiling Caddy with plugins #358586"
