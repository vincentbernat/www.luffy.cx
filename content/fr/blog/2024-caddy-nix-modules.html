---
title: "Personnaliser les modules de Caddy avec Nix"
description: |
  Caddy, un serveur web écrit en Go, peut être étendu à la compilation avec des modules supplémentaires.
  Cet article explique comment faire avec Nix et xcaddy.
uuid: 13b93813-1131-4f0e-8d25-ac6424f395a5
tags:
  - distribution-nixos
attachments:
  "https://github.com/vincentbernat/caddy-nix/": "Dépôt Git"
---

[Caddy][] est un serveur web libre écrit en Go. Il gère automatiquement les
certificats TLS et propose une syntaxe de configuration simple. Les utilisateurs
peuvent étendre ses fonctionnalités via des [modules][plugins] pour ajouter, par
exemple, la [limitation de débit][rate limiting], la [mise en cache][caching] et
l'[intégration à Docker][Docker integration].

Bien que *Caddy* soit disponible dans [Nixpkgs][], l'ajout de modules
supplémentaires n'est pas simple[^simple]. Le processus de compilation nécessite un accès
Internet, ce que [Nix refuse pendant la compilation][Nix denies during build]
pour garantir la [reproductibilité][reproducibility]. Lorsqu'on essaie de
construire la dérivation suivante en utilisant [xcaddy][], un outil pour
compiler Caddy avec des modules supplémentaires, cela échoue avec cette erreur :
`dial tcp: lookup proxy.golang.org on [::1]:53: connection refused`.

[^simple]: C'est une [demande][feature request] depuis un certain temps. Une
    [solution proposée][proposed solution] a été rejetée. Celle décrite dans cet
    article est un peu différente.

```nix
{ pkgs }:
pkgs.stdenv.mkDerivation {
  name = "caddy-with-xcaddy";
  nativeBuildInputs = with pkgs; [ go xcaddy cacert ];
  unpackPhase = "true";
  buildPhase =
    ''
      xcaddy build --with github.com/caddy-dns/powerdns@v1.0.1
    '';
  installPhase = ''
    mkdir -p $out/bin
    cp caddy $out/bin
  '';
}
```

Les dérivations à sortie fixe (*fixed-output derivations*) constituent une
exception à cette règle et obtiennent un accès réseau pendant la compilation.
Elles doivent spécifier leur hash de sortie. Par exemple, la fonction `fetchurl`
produit une dérivation à sortie fixe :

```nix
{ stdenv, fetchurl }:
stdenv.mkDerivation rec {
  pname = "hello";
  version = "2.12.1";
  src = fetchurl {
    url = "mirror://gnu/hello/hello-${version}.tar.gz";
    hash = "sha256-jZkUKv2SV28wsM18tCqNxoCZmLxdYH2Idh9RLibH2yA=";
  };
}
```

Pour créer une dérivation à sortie fixe, vous devez définir l'[attribut
`outputHash`][outputHash]. L'exemple ci-dessous montre comment obtenir le code
source de *Caddy*, avec certains modules activés, comme une dérivation à sortie
fixe en utilisant `xcaddy` et `go mod vendor`.

```nix hl_lines="18 19 20"
pkgs.stdenvNoCC.mkDerivation rec {
  pname = "caddy-src-with-xcaddy";
  version = "2.8.4";
  nativeBuildInputs = with pkgs; [ go xcaddy cacert ];
  unpackPhase = "true";
  buildPhase =
    ''
      export GOCACHE=$TMPDIR/go-cache
      export GOPATH="$TMPDIR/go"
      XCADDY_SKIP_BUILD=1 TMPDIR="$PWD" \
        xcaddy build v${version} --with github.com/caddy-dns/powerdns@v1.0.1
      (cd buildenv* && go mod vendor)
    '';
  installPhase = ''
    mv buildenv* $out
  '';

  outputHash = "sha256-F/jqR4iEsklJFycTjSaW8B/V3iTGqqGOzwYBUXxRKrc=";
  outputHashAlgo = "sha256";
  outputHashMode = "recursive";
}
```

Avec une dérivation à sortie fixe, c'est à nous de nous assurer que la sortie
est toujours la même :

- `xcaddy` ne pas compile le programme et conserve le code source[^xcaddy]
- nous spécifions la version de Caddy que nous voulons construire
- nous spécifions la version de chaque module demandé

[^xcaddy]: Ce n'est pas parfait : si le code source produit par `xcaddy` change,
    le hash changerait et la compilation échouerait.

Vous pouvez utiliser cette dérivation pour remplacer l'attribut `src` dans
`pkgs.caddy` :

```nix
pkgs.caddy.overrideAttrs (prev: {
  src = pkgs.stdenvNoCC.mkDerivation { /* ... */ };
  vendorHash = null;
  subPackages = [ "." ];
});
```

Consultez l'exemple complet dans le [dépôt GitHub][GitHub repository]. Pour une
intégration dans un [Flake][], vous pouvez utiliser `github:vincentbernat/caddy-nix` comme
surcouche à *Nixpkgs* :

```nix
{
  inputs = {
    nixpkgs.url = "nixpkgs";
    flake-utils.url = "github:numtide/flake-utils";
    caddy.url = "github:vincentbernat/caddy-nix";
  };
  outputs = { self, nixpkgs, flake-utils, caddy }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs {
          inherit system;
          overlays = [ caddy.overlays.default ];
        };
      in
      {
        packages = {
          default = pkgs.caddy.withPlugins {
            plugins = [ "github.com/caddy-dns/powerdns@v1.0.1" ];
            hash = "sha256-Vh7JP6RK23Y0E5IDJ3zbBCnF3gKPIav05OMI4ALIcZg=";
          };
        };
      });
}
```

[caddy]: https://caddyserver.com/ "Caddy Web Server"
[plugins]: https://caddyserver.com/docs/modules/ "All registered Caddy plugins"
[rate limiting]: https://github.com/mholt/caddy-ratelimit "HTTP rate limiting module for Caddy"
[caching]: https://github.com/caddyserver/cache-handler "Distributed HTTP caching module for Caddy"
[docker integration]: https://github.com/invzhi/caddy-docker-upstreams "Docker dynamic upstreams for Caddy"
[nixpkgs]: https://github.com/NixOS/nixpkgs/blob/master/pkgs/by-name/ca/caddy/package.nix
[xcaddy]: https://github.com/caddyserver/xcaddy "Build Caddy with plugins"
[reproducibility]: https://zero-to-nix.com/concepts/reproducibility "Zero to Nix: reproducibility"
[nix denies during build]: https://zero-to-nix.com/concepts/hermeticity "Zero to Nix: hermeticity"
[flake]: https://zero-to-nix.com/concepts/flakes "Zero to Nix: flakes"
[outputhash]: https://nix.dev/manual/nix/2.22/language/advanced-attributes.html#adv-attr-outputHash
[github repository]: https://github.com/vincentbernat/caddy-nix/blob/main/examples/xcaddy-src/flake.nix
[feature request]: https://github.com/NixOS/nixpkgs/issues/14671 "caddy: add all addons #14671"
[proposed solution]: https://github.com/NixOS/nixpkgs/pull/317881 "caddy: add support for compiling with Caddy modules (plugins) #317881"
